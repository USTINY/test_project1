&НаКлиенте
Процедура ЗаполнитьОстатками(Команда)
	
	Если Объект.Проведен Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Для заполнения требуется отменить проведение документа.";
		Сообщение.Сообщить();
		Возврат;	
	КонецЕсли;  
	
	Объект.СписокСотрудников.Очистить();
	ЗаполнитьОстаткамиНаСервере();   
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткамиНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник,
		|	ВзаиморасчетыССотрудникамиОстатки.ВидРасчета КАК ВидРасчета,
		|	СУММА(ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыССотрудниками.Остатки(&Период, Подразделение = &Подразделение) КАК ВзаиморасчетыССотрудникамиОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВзаиморасчетыССотрудникамиОстатки.Сотрудник,
		|	ВзаиморасчетыССотрудникамиОстатки.ВидРасчета";
	
	Запрос.УстановитьПараметр("Период", Новый Граница(КонецМесяца(Объект.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(Объект.СписокСотрудников.Добавить(), Выборка);
	КонецЦикла;

КонецПроцедуры

